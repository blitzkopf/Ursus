CREATE OR REPLACE TRIGGER DDL_EVENT_TRIGGER after
DDL on database
declare
    l_enqueue_options    dbms_aq.enqueue_options_t;
    l_message_properties dbms_aq.message_properties_t;
    l_message_handle     RAW(16);
    l_msg ddl_event_type;
    l_sql_txt ora_name_list_t;
    n number;
    l_sql_txt_clob CLOB;
    l_sql_txt_string varchar2(4000);
  begin
    n := ora_sql_txt(l_sql_txt);
    if(n>0) then
      l_sql_txt_clob := l_sql_txt(1);
      for i in 2..n loop
        dbms_lob.append(l_sql_txt_clob,l_sql_txt(i));
      end loop;
    ELSE
      l_sql_txt_clob := null;
    end if;

    l_msg := ddl_event_type( ora_sysevent ,ora_login_user , sys_context('USERENV','OS_USER'),
			ora_instance_num,ora_database_name,ora_dict_obj_owner ,
      ora_dict_obj_name,ora_dict_obj_type, l_sql_txt_clob);

    DBMS_AQ.ENQUEUE(
      queue_name         => 'URSUS.AQ_DDL_EVENT_QUEUE',
      enqueue_options    => l_enqueue_options,
      message_properties => l_message_properties,
      payload            => l_msg,
      msgid              => l_message_handle
    );

   --COMMIT;

end DDL_EVENT_TRIGGER;

/
ALTER TRIGGER "URSUS"."DDL_EVENT_TRIGGER" ENABLE;
